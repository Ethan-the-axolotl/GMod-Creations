@name Teleporting Door
#[
    Author: Matthew (FaceInCake) Eppel
    Date: Jan 10, 2021
    Copyright (c) MIT 2021 Matthew Eppel
    
    Simply place this E2 onto a train to have rail tracks
    be created infront of the train at all times. You can
    also use A & D to turn, and sprint to make shorter distances.
    walk will make you go up, sprint+walk goes down.
]#
@model models/props_interiors/elevatorshaft_door01a.mdl
@persist Door:entity Cooldown:number DOOR_HEIGHT:number

if (chatClk()) {
    if (lastSpoke()==owner()) {
        local Msg = lastSaid():trim():lower()
        if (Msg == "summon thy door") {
            if (Door) {
                Door:propDelete()
            }
            Door = propSpawn(
                "models/props_interiors/elevatorshaft_door01a.mdl",
                owner():pos() + owner():forward()*64 + vec(0, 0, DOOR_HEIGHT),
                (-owner():forward()):toAngle(),
            1)
            Door:propFreeze(1)
        }
        elseif (Msg == "recall thy door") {
            if (Door) {
                Door:propDelete()
            }
        }
    }
}
elseif (keyClk()) {
    if (curtime() > Cooldown) {
        local Player = keyClk()
        if (keyClk(Player) == 1) {
            if (Player:aimEntity() == Door) {
                if (Player:aimPos():distance2(Player:pos()) < 80*80) {
                    Player:teleport(entity():pos() + entity():forward()*48 - vec(0,0,DOOR_HEIGHT))
                    local D = soundDuration("ambient/machines/teleport1.wav")
                    entity():soundPlay(1, D, "ambient/machines/teleport1.wav")
                    Door:soundPlay(2, D, "ambient/machines/teleport1.wav")
                    Cooldown = curtime() + 1.0
                }
            }
        }
    }
}
elseif (useClk()) {
    if (curtime() > Cooldown) {
        local Player = useClk()
        if (Door) {
            Player:teleport(Door:pos() + Door:forward()*48 - vec(0,0,DOOR_HEIGHT))
            local D = soundDuration("ambient/machines/teleport1.wav")
            entity():soundPlay(1, D, "ambient/machines/teleport1.wav")
            Door:soundPlay(2, D, "ambient/machines/teleport1.wav")
            Cooldown = curtime() + 1.0
        }
    }
}
elseif (playerConnectClk()) {
    runOnKeys(lastConnectedPlayer(), 1, array("e"))
}
elseif (first()) {
    Door = noentity()
    Cooldown = 0
    DOOR_HEIGHT = 108.3796081543 / 2
    propSpawnUndo(0)
    propSpawnEffect(1)
    runOnChat(1)
    runOnUse(1)
    runOnKeys(players(), 1, array("e"))
    runOnPlayerConnect(1)
    entity():removeNoCollideAll()
    entity():propFreeze(1)
}
