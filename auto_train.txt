@name Auto Train
#[
    Author: Matthew (FaceInCake) Eppel
    Date: Apr 29, 2020
    (c) Notice: Free to take and share. Don't claim false ownership. Don't remove this notice.
    
    Simply place this E2 onto a train to have rail tracks
    be created infront of the train at all times. You can
    also use A & D to turn, and sprint to make shorter distances.
    walk will make you go up, sprint+walk goes down.
]#

# The train vehicle and pod controller entity
@persist Train:entity
# The train track model names
@persist [Straight StraightS Turn TurnS Up Down]:string
# The train track model lengths
@persist StraightLen StraightSLen TurnLen TurnSLen RampLen RampLenZ
# The next track position and direction, and backwards direction
@persist Next:vector Dir:angle

function createUpPiece() {
    propSpawn(Up, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*RampLen + Dir:up()*RampLenZ
    Dir = Dir:rotateAroundAxis(Dir:right(),  22.5)
}

function createDownPiece() {
    propSpawn(Down, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*RampLen - Dir:up()*RampLenZ
    Dir = Dir:rotateAroundAxis(Dir:right(), -22.5)
}

function createLeftPiece() {
    Pos = Next + Dir:forward()*TurnLen - Dir:right()*TurnLen
    propSpawn(Turn, Pos, Dir:rotateAroundAxis(Dir:up(), 90), 1):propStatic(1)
    Next += Dir:forward()*TurnLen - Dir:right()*TurnLen
    Dir = Dir:rotateAroundAxis(Dir:up(), 90)
}

function createLeftShortPiece() {
    Pos = Next + Dir:forward()*TurnSLen - Dir:right()*TurnSLen
    propSpawn(TurnS, Pos, Dir:rotateAroundAxis(Dir:up(), 90), 1):propStatic(1)
    Next += Dir:forward()*TurnSLen - Dir:right()*TurnSLen
    Dir = Dir:rotateAroundAxis(Dir:up(), 90)
}

function createRightPiece() {
    propSpawn(Turn, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*TurnLen + Dir:right()*TurnLen
    Dir = Dir:rotateAroundAxis(Dir:up(), -90)
}

function createRightShortPiece() {
    propSpawn(TurnS, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*TurnSLen + Dir:right()*TurnSLen
    Dir = Dir:rotateAroundAxis(Dir:up(), -90)
}

function createStraightPiece() {
    propSpawn(Straight, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*StraightLen
}

function createStraightShortPiece() {
    propSpawn(StraightS, Next, Dir:rotateAroundAxis(Dir:up(), 180), 1):propStatic(1)
    Next += Dir:forward()*StraightSLen
}

if (clk()) {
    Dist = Train:pos():distance2(Next)
    if (Dist < Train:radius()*Train:radius()+128) {
        if (Train:driver():keySprint()) {
            if (Train:driver():keyLeft() & !Train:driver():keyRight()) {
                createLeftShortPiece()
            }
            elseif (Train:driver():keyRight() & !Train:driver():keyLeft()) {
                createRightShortPiece()
            }
            elseif (Train:driver():keyWalk()) {
                createDownPiece()
            } else {
                createStraightShortPiece()
            }
        } else {
            if (Train:driver():keyLeft() & !Train:driver():keyRight()) {
                createLeftPiece()
            }
            elseif (Train:driver():keyRight() & !Train:driver():keyLeft()) {
                createRightPiece()
            }
            elseif (Train:driver():keyWalk()) {
                createUpPiece()
            } else {
                createStraightPiece()
            }
        }
    }
    interval(32)
} elseif (first()) {
    Train = entity():getConnectedEntities()[2, entity]
    Train:setPos(Train:pos() + vec(0, 0, 32))
    propSpawnEffect(0)
    Straight = "models/sprops/trans/train/track_s05.mdl"
    StraightLen = 1295
    StraightS = "models/sprops/trans/train/track_s02.mdl"
    StraightSLen = 324
    Turn = "models/sprops/trans/train/track_t90_02.mdl"
    TurnLen = 1650
    TurnS = "models/sprops/trans/train/track_t90_01.mdl"
    TurnSLen = 825
    Up = "models/sprops/trans/train/track_h02.mdl"
    Down = "models/sprops/trans/train/track_h03.mdl"
    RampLen = 1262
    RampLenZ = 254
    Dir = Train:forward():toAngle():setPitch(0):setRoll(0)
    Next = Train:pos() + vec(0, 0, 4)
    propSpawn(Straight, Next, Dir, 1):propStatic(1)
    propSpawn(Straight, Next, Dir:setYaw(Dir:yaw()+180), 1):propStatic(1)
    Next += Dir:forward()*StraightLen
    interval(32)
}
